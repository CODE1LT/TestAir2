apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'
apply plugin: "androidx.navigation.safeargs.kotlin"
//apply plugin: 'com.google.gms.google-services'
//apply plugin: 'com.google.firebase.crashlytics'

def versionMajor = 1
def versionMinor = 0
def buildId = System.getenv("BUILD_BUILDID") as Integer ?: 1
def computedVersionName = String.format('%d.%d.', versionMajor, versionMinor)
def computedVersionCode = (versionMajor * 100000) + (versionMinor * 10000) + buildId

def keystorePropertiesFile = rootProject.file("keystore.properties")
def keystoreProperties = new Properties()
keystoreProperties.load(new FileInputStream(keystorePropertiesFile))

android {

    signingConfigs {
        debug {
            storeFile file("debug.keystore")
            keyAlias 'androiddebugkey'
            storePassword 'android'
            keyPassword 'android'
        }
        release {
            keyAlias keystoreProperties['keyAlias']
            keyPassword keystoreProperties['keyPassword']
            storeFile file(keystoreProperties['storeFile'])
            storePassword keystoreProperties['storePassword']
        }
    }

    compileSdkVersion 30
    defaultConfig {
        applicationId "com.code1.testair2"
        minSdkVersion 21
        targetSdkVersion 30
        versionCode computedVersionCode
        versionName computedVersionName
        testInstrumentationRunner "com.code1.testair2.TestRunner"
        applicationVariants.all { variant ->
            variant.outputs.all { output ->
                outputFileName = "TestAir2-${variant.versionName}.apk"
            }
        }
        multiDexEnabled true
        javaCompileOptions {
            annotationProcessorOptions {
                arguments = [
                        "room.schemaLocation"  : "$projectDir/schemas".toString(),
                        "room.incremental"     : "true",
                        "room.expandProjection": "true"]
            }
        }
    }

    buildFeatures {
        dataBinding = true
    }

    kotlinOptions {
        jvmTarget = '1.8'
    }

    sourceSets {
        test {
            resources.srcDirs += ['src/test/resources']
        }
    }

    buildTypes {
        mock {
            signingConfig signingConfigs.debug
            debuggable true
            manifestPlaceholders = [
                    //crashlyticsCollectionEnabled: "false",
                                    networkSecurityConfig: "@xml/cleartext_traffic_permited"]
            versionNameSuffix "-mock"
        }
        check {
            signingConfig signingConfigs.debug
            debuggable true
            manifestPlaceholders = [
                    //crashlyticsCollectionEnabled: "true",
                                    networkSecurityConfig: "@xml/cleartext_traffic_forbidden"]
            applicationIdSuffix ".test"
            versionNameSuffix "-test"
        }
        debug {
            signingConfig signingConfigs.debug
            debuggable true
            manifestPlaceholders = [
                    //crashlyticsCollectionEnabled: "false",
                                    networkSecurityConfig: "@xml/cleartext_traffic_permited"]
            applicationIdSuffix ".debug"
            versionNameSuffix "-debug"
        }
        release {
            //Enable signing config only for local building (add keystore & update keystore.properties)
            //signingConfig signingConfigs.release
            debuggable false
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile(
                    'proguard-android-optimize.txt'),
                    'proguard-rules.pro'
            manifestPlaceholders = [
                    //crashlyticsCollectionEnabled: "true",
                                    networkSecurityConfig: "@xml/cleartext_traffic_forbidden"]
            versionNameSuffix "-release"
        }
    }
    compileOptions {
        coreLibraryDesugaringEnabled true
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    buildToolsVersion '30.0.2'

    configurations {
        all*.exclude group: 'com.google.guava', module: 'listenablefuture'
    }
}

dependencies {
    def room_version = "2.2.5"
    def lifecycle_version = "2.2.0"
    def nav_version = "2.3.1"
    def dagger_version = "2.27"
    def okhttp_version = '4.2.2'
    def retrofit_version = "2.6.2"
    def work_version = "2.4.0"
    def mockito_version = '3.2.4'
    def mockito_kotlin_version = '2.2.0'
    def android_test_version = '1.3.0'
    def espresso_version = '3.3.0'

    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
    implementation 'androidx.core:core-ktx:1.3.2'
    implementation "androidx.annotation:annotation:1.1.0"
    implementation 'androidx.appcompat:appcompat:1.2.0'
    implementation 'com.google.android.material:material:1.2.1'
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:$lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-livedata-ktx:$lifecycle_version"
    implementation "android.arch.lifecycle:extensions:$lifecycle_version"
    implementation "android.arch.lifecycle:viewmodel:$lifecycle_version"
    implementation "androidx.work:work-runtime-ktx:$work_version"
    implementation "androidx.viewpager2:viewpager2:1.0.0"
    implementation 'com.google.firebase:firebase-messaging:20.3.0'
    implementation 'com.google.firebase:firebase-analytics-ktx:17.6.0'
    //implementation 'com.google.firebase:firebase-crashlytics:17.2.2'
    coreLibraryDesugaring 'com.android.tools:desugar_jdk_libs:1.0.10'
    androidTestImplementation 'androidx.fragment:fragment-testing:1.2.5'
    implementation 'androidx.fragment:fragment-ktx:1.2.5'
    implementation 'com.jakewharton.threetenabp:threetenabp:1.2.4'
    testImplementation 'org.threeten:threetenbp:1.4.4'

    // Tests
    androidTestImplementation 'junit:junit:4.13.1'
    androidTestImplementation "androidx.test:core:$android_test_version"
    androidTestImplementation "androidx.test:runner:$android_test_version"
    androidTestImplementation "androidx.test:rules:$android_test_version"
    androidTestImplementation "androidx.test.ext:truth:$android_test_version"
    androidTestImplementation 'androidx.test.ext:junit:1.1.2'
    androidTestImplementation "androidx.test.espresso:espresso-core:$espresso_version"
    androidTestImplementation "androidx.test.espresso:espresso-intents:$espresso_version"
    implementation 'com.google.truth:truth:1.0'
    androidTestImplementation "org.mockito:mockito-android:$mockito_version"
    testImplementation "com.nhaarman.mockitokotlin2:mockito-kotlin:$mockito_kotlin_version"

    // Visual
    implementation 'androidx.cardview:cardview:1.0.0'
    implementation 'androidx.recyclerview:recyclerview:1.1.0'
    implementation 'com.airbnb.android:lottie:3.3.1'
    implementation 'com.github.bumptech.glide:glide:4.9.0'

    // Navigation
    implementation "androidx.navigation:navigation-fragment-ktx:$nav_version"
    implementation "androidx.navigation:navigation-ui-ktx:$nav_version"

    // Dagger 2
    implementation "com.google.dagger:dagger:$dagger_version"
    implementation "com.google.dagger:dagger-android-support:2.25.3"
    implementation "com.google.dagger:dagger-android:2.25.3"
    kapt "com.google.dagger:dagger-android-processor:$dagger_version"
    kapt "com.google.dagger:dagger-compiler:$dagger_version"
    kaptAndroidTest "com.google.dagger:dagger-compiler:$dagger_version"

    // Networking
    implementation "com.squareup.okhttp3:okhttp:$okhttp_version"
    implementation "com.squareup.okhttp3:logging-interceptor:$okhttp_version"
    implementation "com.squareup.retrofit2:retrofit:$retrofit_version"
    implementation "com.squareup.retrofit2:converter-gson:$retrofit_version"
    implementation "com.squareup.okhttp3:mockwebserver:$okhttp_version"

    // Timber
    implementation 'com.jakewharton.timber:timber:4.7.1'

    // Room
    implementation "androidx.room:room-runtime:$room_version"
    kapt "androidx.room:room-compiler:$room_version"
    implementation "androidx.room:room-ktx:$room_version"

    // Date
    implementation 'com.jakewharton.threetenabp:threetenabp:1.2.4'
}